// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:

    Inc16(in=presentOut, out=incrementedOut);
    Mux16(a=incrementedOut, b=in, sel=load, out=loadOrIncOut);
    Mux16(a=loadOrIncOut, b=false, sel=reset, out=resetOrLoadOut);

    Or(a=load, b=inc, out=loadOrInc);
    Or(a=loadOrInc, b=reset, out=anyAction);

    Mux16(a=presentOut, b=resetOrLoadOut, sel=anyAction, out=registerIn);
    Register(in=registerIn, load=anyAction, out=out, out=presentOut);
}
